'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' Copyright 2008,2011 Jukka Zitting <jukka.zitting@gmail.com>               '
'                                                                           '
' Licensed under the Apache License, Version 2.0 (the "License");           '
' you may not use this file except in compliance with the License.          '
' You may obtain a copy of the License at                                   '
'                                                                           '
'     http://www.apache.org/licenses/LICENSE-2.0                            '
'                                                                           '
' Unless required by applicable law or agreed to in writing, software       '
' distributed under the License is distributed on an "AS IS" BASIS,         '
' WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.  '
' See the License for the specific language governing permissions and       '
' limitations under the License.                                            '
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

' Application constants
const DEFAULTEPOCH      := 5.0;    ' Default epoch is five seconds
const DEFAULTLEVELS%    := 1;      ' Default is just one score level
const MAXSCORELEVELS%   := 5;

' Constants values for Spike method calls
const NEWCHANNEL%       := 0;      ' ChanNew
const WAVEFORM%         := 1;      ' ChanNew
const TEXTMARK%         := 8;      ' ChanNew
const DEFAULTBUFFER%    := 0;      ' ChanNew
const PRIMARYCOLOUR%    := 1;      ' ChanColour
const BLACK%            := 1;      ' ChanColour
const RED%              := 16;     ' ChanColour
const SKYLINE%          := 16;     ' DrawMode
const CUSTOMLABEL%      := 4;      ' HCursorLabel
const MEAN%             := 2;      ' ChanMeasure
const OK%               := 1;      ' DlgShow
const MOVERESIZE%       := 0x0004; ' Toolbar
const CANUSEVIEWMENU%   := 0x0020; ' Toolbar
const NOHCURSORCHANNEL% := 0x1000; ' Toolbar
const TEXTFILE%         := 1;      ' FileNew

var emg%;
var epoch;
var start;

var epochCount%;
var epochSize%;

var power%;
var score%;

var scoreLevels%, level[MAXSCORELEVELS%], cursor%[MAXSCORELEVELS%];

proc CreatePowerChannel()
var data[epochCount%], emgPerEpoch[epochSize%], i%, n%;
power% := ChanNew(NEWCHANNEL%, WAVEFORM%, DEFAULTBUFFER%, epoch);
ChanTitle$(power%, "EMG power");
ChanColour(power%, PRIMARYCOLOUR%, RED%);
ChanWeight(power%, ChanWeight(emg%) / 2.0);
DrawMode(power%, SKYLINE%);
for i% := 0 to epochCount% - 1 do
    n% := ChanData(emg%, emgPerEpoch, start + i% * epoch, MaxTime(emg%));
    if (n% > 0) then
        data[i%] := ArrDot(emgPerEpoch[:n%], emgPerEpoch[:n%]) * BinSize(emg%);
    else
        data[i%] := 0.0;
    endif;
next;
ChanWriteWave(power%, data, start);
YRange(power%, 0.0, ChanMeasure(power%, MEAN%, 0.0, MaxTime(power%)) * 2.0);
ChanShow(power%);
end;

proc CreatePowerCursors()
var i%;
for i% := 0 to scoreLevels% - 1 do
    level[i%] := (i% + 1) * YHigh(power%) / (scoreLevels% + 1);
    cursor%[i%] := HCursorNew(power%, level[i%]);
    HCursorLabel(CUSTOMLABEL%, cursor%[i%], Print$("Score level %d: %%p", i% + 1));
next;
end;

proc CreateScoreChannel()
score% := ChanNew(NEWCHANNEL%, WAVEFORM%, DEFAULTBUFFER%, epoch);
ChanTitle$(score%, "EMG score");
ChanColour(score%, PRIMARYCOLOUR%, BLACK%);
ChanWeight(score%, ChanWeight(emg%) / 5.0);
DrawMode(score%, SKYLINE%);
YRange(score%, 0.0, 2.0);
ChanShow(score%);
end;

proc UpdateScoreChannel()
var data[epochCount%], i%, j%, n%, value;
n% := ChanData(power%, data, start, MaxTime(power%));
for i% := 0 to epochCount% - 1 do
    value := 0.0;
    if (i% < n%) then
        for j% := 0 to scoreLevels% - 1 do
            if data[i%] > level[j%] then
                value := j% + 1;
            endif;
        next;
    endif;
    data[i%] := value;
next;
ChanWriteWave(score%, data, start);
end;

func RefreshScoreIfCursorChanged%()
var refresh%, i%;
refresh% := 0;
for i% := 0 to scoreLevels% - 1 do
    if level[i%] <> HCursor(cursor%[i%]) then
        level[i%] := HCursor(cursor%[i%]);
        refresh% := 1;
    endif;
next;
if refresh% = 1 then
    UpdateScoreChannel();
endif;
return 1;
end;

proc PrintScoreReport()
var powerData[epochCount%], scoreData[epochCount%], i%, n%, file$;
file$ := FileName$();
if Right$(file$, 4) = ".smr" then
    file$ := Left$(file$, Len(file$) - 4);
endif;
file$ := Print$("%s-emgscore.txt", file$);
n% := ChanData(power%, powerData, 0.0, MaxTime(power%));
n% := ChanData(score%, scoreData, 0.0, MaxTime(score%));
FileNew(TEXTFILE%);
Print("EMG Score\n");
Print("Epoch count\t%d\n", epochCount%);
Print("Epoch length\t%f\n", epoch);
Print("Score levels");
for i% := 0 to scoreLevels% - 1 do
    Print("\t%f", level[i%]);
next;
Print("\n");
print("\n");
print("Time\tPower\tScore\n");
for i% := 0 to n% - 1 do
    Print("%d\t%f\t%1.0f\n", i% * epoch, powerData[i%], scoreData[i%]);
next;
FileSaveAs(file$);
FileClose();
end;

proc CreateEpochsChannel()
var scoreData[epochCount%], i%, n%;
n% := ChanData(score%, scoreData, 0.0, MaxTime(score%));

var ch%;
ch% := MemChan(TEXTMARK%, 40);
ChanTitle$(ch%, "Epochs");
ChanComment$(ch%, "Inactive|Active|");

var comment$[9], code%[4];
comment$[0] := "Inactive";
comment$[1] := "Active";
comment$[2] := "2";
comment$[3] := "3";
comment$[4] := "4";
comment$[5] := "5";
comment$[6] := "6";
comment$[7] := "7";
comment$[8] := "no stage";
for i% := 0 to n% - 1 do
    code%[0] := Round(scoreData[i%]);
    Message("%d %f", code%[0], scoreData[i%]);
    MemSetItem(ch%, 0, i% * epoch, code%[], comment$[code%[0]]);
next;
code%[0] := 8;
MemSetItem(ch%, 0, epochCount% * epoch, code%[], comment$[code%[0]]);

const STATEMODE% := 15;
const SHOWTEXT%  := 2;
var epochsChannel%;
epochsChannel% := ChanNew(0, TEXTMARK%, 40);
MemSave(ch%, epochsChannel%, TEXTMARK%, 1);
ChanDelete(ch%);
DrawMode(epochsChannel%, STATEMODE%, SHOWTEXT%);
ChanShow(epochsChannel%);
end;


' Find the time view window

' Get the list of visible channels and select an "EMG" channel as the default
var channels%[100];
ChanList(channels%, 0x801);
if channels%[0] > 0 then
    emg% := channels%[1];
    var i%;
    for i% := 2 to channels%[0] do
        if (InStr(ChanTitle$(channels%[i%]), "EMG") > 0) then
            emg% := channels%[i%];
        endif;
    next;
endif;

epoch := DEFAULTEPOCH;
scoreLevels% := DEFAULTLEVELS%;

DlgCreate("EMG Score Settings");
DlgChan(1, "EMG Channel", channels%);
DlgReal(2, "Epoch (s)", 0.1, 100000.0);
DlgReal(3, "Start time (s)", 0.0, MaxTime(emg%));
DlgInteger(4, "Score levels (1-2)", 1, 2);
if DlgShow(emg%, epoch, start, scoreLevels%) = OK% then
    epochCount% := (MaxTime(emg%) - start) / epoch;  ' Total number of epochs
    epochSize% := epoch / BinSize(emg%);             ' Number of data points per epoch
    
    CreatePowerChannel();
    CreatePowerCursors();
    CreateScoreChannel();
    
    ' Create the score channel    
    UpdateScoreChannel();
    
    ' Start at the given point on the X axis
    XRange(start);   
    
    var button%;
    ToolbarSet(0, "Refresh", RefreshScoreIfCursorChanged%);
    ToolbarSet(1, "Cancel");
    ToolbarSet(2, "Report to file");
    ToolbarSet(3, "Report to channel");
    button% := Toolbar(
        "Set the score limits by moving the horizontal cursors",
        MOVERESIZE% + CANUSEVIEWMENU% + NOHCURSORCHANNEL%);
    docase
    case button% = 1 then
        Message("EMG score cancelled.");
    case button% = 2 then
        PrintScoreReport();
    case button% = 3 then
        CreateEpochsChannel();
    endcase;
    
    ChanDelete(score%);
    for i% := 0 to scoreLevels% - 1 do
        HCursorDelete(cursor%[i%]);
    next;
    ChanDelete(power%);
endif;
